defmodule Bedrock.ControlPlane.Director.Recovery.ResolverStartupPhase do
  @moduledoc """
  Solves the critical concurrency control challenge by starting resolver components
  that implement MVCC conflict detection.

  Transforms resolver descriptors from vacancy creation into operational resolver
  processes that are immediately ready to handle transaction conflict detection.

  Uses round-robin distribution across resolution-capable nodes from
  `context.node_capabilities.resolution`, ensuring fault tolerance by spreading
  resolvers across different machines. Resolvers start directly in running mode
  without requiring recovery coordination.

  Stalls if no resolution-capable nodes are available or if individual resolver
  startup fails since conflict detection is fundamental to transaction isolation
  guarantees.

  """

  use Bedrock.ControlPlane.Director.Recovery.RecoveryPhase

  alias Bedrock.ControlPlane.Config.ResolverDescriptor
  alias Bedrock.ControlPlane.Director.Recovery.Shared
  alias Bedrock.DataPlane.Resolver

  require Logger

  @impl true
  def execute(recovery_attempt, context) do
    start_supervised_fn =
      Map.get(context, :start_supervised_fn, fn child_spec, node ->
        sup_otp_name = recovery_attempt.cluster.otp_name(:sup)
        starter_fn = Shared.starter_for(sup_otp_name)
        starter_fn.(child_spec, node)
      end)

    available_resolver_nodes = Map.get(context.node_capabilities, :coordination, [])
    {_first_version, log_last_version} = recovery_attempt.version_vector
    last_committed_version = Shared.max_committed_version(log_last_version, context)

    if last_committed_version != log_last_version do
      Logger.info(
        "PATCHED (fuu): start resolvers at max of log and materializer versions; log=#{inspect(log_last_version)} resolvers=#{inspect(last_committed_version)}"
      )
    end

    resolver_context = %{
      resolvers: recovery_attempt.resolvers,
      epoch: recovery_attempt.epoch,
      available_nodes: available_resolver_nodes,
      start_supervised_fn: start_supervised_fn,
      lock_token: context.lock_token,
      last_committed_version: last_committed_version,
      cluster: recovery_attempt.cluster
    }

    resolver_context
    |> define_resolvers()
    |> case do
      {:error, reason} ->
        {recovery_attempt, {:stalled, reason}}

      {:ok, resolvers} ->
        updated_recovery_attempt = %{recovery_attempt | resolvers: resolvers}

        {updated_recovery_attempt, Bedrock.ControlPlane.Director.Recovery.TopologyPhase}
    end
  end

  @spec define_resolvers(%{
          resolvers: [ResolverDescriptor.t()],
          epoch: Bedrock.epoch(),
          available_nodes: [node()],
          start_supervised_fn: (Supervisor.child_spec(), node() ->
                                  {:ok, pid()} | {:error, term()}),
          lock_token: Bedrock.lock_token(),
          last_committed_version: Bedrock.version(),
          cluster: module()
        }) ::
          {:ok, [{start_key :: Bedrock.key(), resolver :: pid()}]}
          | {:error, {:failed_to_start, :resolver, node(), reason :: term()}}
  def define_resolvers(context) do
    if Enum.empty?(context.available_nodes) and not Enum.empty?(context.resolvers) do
      {:error, {:insufficient_nodes, :no_coordination_capable_nodes, length(context.resolvers), 0}}
    else
      resolver_boot_info =
        context.resolvers
        |> generate_resolver_ranges()
        |> Enum.map(fn [start_key, end_key] ->
          key_range = {start_key, end_key}

          {child_spec_for_resolver(
             context.epoch,
             key_range,
             context.lock_token,
             context.last_committed_version,
             self(),
             context.cluster
           ), start_key}
        end)

      start_resolvers(
        resolver_boot_info,
        context.available_nodes,
        context.start_supervised_fn
      )
    end
  end

  @spec generate_resolver_ranges([ResolverDescriptor.t()]) :: [[Bedrock.key()]]
  defp generate_resolver_ranges(resolvers) do
    resolvers
    |> Enum.map(& &1.start_key)
    |> Enum.sort()
    |> Enum.concat([Bedrock.end_of_keyspace()])
    |> Enum.chunk_every(2, 1, :discard)
  end

  @spec start_resolvers(
          resolver_boot_info :: [
            {Supervisor.child_spec(), start_key :: Bedrock.key()}
          ],
          available_nodes :: [node()],
          start_supervised :: (Supervisor.child_spec(), node() -> {:ok, pid()} | {:error, term()})
        ) ::
          {:ok, [{start_key :: Bedrock.key(), resolver :: pid()}]}
          | {:error, {:failed_to_start, :resolver, node(), reason :: term()}}
  def start_resolvers(resolver_boot_info, available_nodes, start_supervised) do
    # PATCHED (fuu): start resolvers sequentially in the director process.
    available_nodes
    |> Stream.cycle()
    |> Enum.zip(resolver_boot_info)
    |> Enum.reduce_while([], fn {node, {child_spec, start_key}}, resolvers ->
      case start_supervised.(child_spec, node) do
        {:ok, resolver} -> {:cont, [{start_key, resolver} | resolvers]}
        {:error, reason} -> {:halt, {:error, {:failed_to_start, :resolver, node, reason}}}
      end
    end)
    |> case do
      {:error, reason} -> {:error, reason}
      resolvers -> {:ok, Enum.sort_by(resolvers, &elem(&1, 0))}
    end
  end

  @spec child_spec_for_resolver(
          epoch :: Bedrock.epoch(),
          key_range :: Bedrock.key_range(),
          lock_token :: Bedrock.lock_token(),
          last_committed_version :: Bedrock.version(),
          director :: pid(),
          cluster :: module()
        ) ::
          Supervisor.child_spec()
  def child_spec_for_resolver(epoch, key_range, lock_token, last_committed_version, director, cluster) do
    Resolver.Server.child_spec(
      lock_token: lock_token,
      epoch: epoch,
      key_range: key_range,
      last_version: last_committed_version,
      director: director,
      cluster: cluster
    )
  end
end
