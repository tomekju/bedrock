defmodule Bedrock.ControlPlane.Coordinator.Server do
  @moduledoc false
  use GenServer

  import Bedrock.ControlPlane.Coordinator.DirectorManagement,
    only: [
      try_to_start_director: 1,
      handle_director_failure: 3,
      cleanup_director_on_leadership_loss: 1
    ]

  import Bedrock.ControlPlane.Coordinator.Durability,
    only: [
      durably_write_service_registration: 3,
      durable_write_completed: 3
    ]

  import Bedrock.ControlPlane.Coordinator.State.Changes,
    only: [
      put_leader_node: 2,
      put_epoch: 2,
      put_leader_startup_state: 2,
      put_config: 2,
      put_transaction_system_layout: 2,
      update_raft: 2,
      add_tsl_subscriber: 2,
      remove_tsl_subscriber: 2,
      check_for_recovery_capability_changes: 1,
      update_recovery_capability_hash: 1
    ]

  import Bedrock.ControlPlane.Coordinator.Telemetry,
    only: [
      trace_started: 2,
      trace_election_completed: 1,
      trace_consensus_reached: 1,
      trace_leader_ready_starting_director: 1,
      trace_recovery_capability_change_detected: 0,
      trace_recovery_retry_attempt: 1,
      trace_recovery_failed: 1
    ]

  import Bedrock.Internal.GenServer.Replies

  alias Bedrock.ControlPlane.Coordinator.Commands
  alias Bedrock.ControlPlane.Coordinator.DiskRaftLog
  alias Bedrock.ControlPlane.Coordinator.RaftAdapter
  alias Bedrock.ControlPlane.Coordinator.State
  alias Bedrock.ObjectStorage
  alias Bedrock.ObjectStorage.LocalFilesystem
  alias Bedrock.Raft
  alias Bedrock.Raft.Log
  alias Bedrock.Raft.Log.InMemoryLog
  alias Bedrock.Raft.Log.TupleInMemoryLog
  alias Bedrock.SystemKeys.ClusterBootstrap

  require Logger

  # The FlatBuffer-generated ClusterBootstrap.read/1 can return errors per the library spec,
  # but Dialyzer doesn't see this because the generated wrapper lacks a @spec.
  # Suppress the false positive since defensive error handling is appropriate here.
  @dialyzer {:no_match, parse_bootstrap_data: 2}

  @spec child_spec(opts :: [cluster: module()]) :: Supervisor.child_spec()
  def child_spec(opts) do
    cluster = opts[:cluster] || raise "Missing :cluster option"
    otp_name = cluster.otp_name(:coordinator)

    %{
      id: __MODULE__,
      start:
        {GenServer, :start_link,
         [
           __MODULE__,
           {cluster, otp_name},
           [name: otp_name]
         ]},
      restart: :permanent
    }
  end

  @impl true
  def init({cluster, otp_name}) do
    trace_started(cluster, otp_name)

    my_node = Node.self()

    with {:ok, coordinator_nodes} <- cluster.fetch_coordinator_nodes(),
         true <- my_node in coordinator_nodes || {:error, :not_a_coordinator},
         {:ok, raft_log} <- init_raft_log(cluster),
         {:ok, {loaded_epoch, loaded_config, loaded_old_tsl}} <-
           load_state_from_object_storage(cluster) do
      # PATCHED (fuu): fail closed when coordinator bootstrap cannot be proven.
      # The only nil-config state is a backend-validated first boot.
      {:ok,
       %State{
         cluster: cluster,
         my_node: my_node,
         otp_name: otp_name,
         supervisor_otp_name: cluster.otp_name(:sup),
         epoch: loaded_epoch,
         config: loaded_config,
         old_transaction_system_layout: loaded_old_tsl,
         transaction_system_layout: nil,
         raft:
           Raft.new(
             my_node,
             Enum.reject(coordinator_nodes, &(&1 == my_node)),
             raft_log,
             RaftAdapter
           ),
         last_durable_txn_id: Log.initial_transaction_id(raft_log)
       }, {:continue, :check_recovery_consensus}}
    else
      {:error, :unavailable} -> :ignore
      {:error, :not_a_coordinator} -> :ignore
      {:error, reason} -> {:stop, {:bootstrap_load_failed, reason}}
    end
  end

  @impl true
  def handle_continue(:check_recovery_consensus, t) do
    # Check if this is a single-node cluster that needs recovery consensus
    if Raft.am_i_the_leader?(t.raft) and t.raft.quorum == 0 do
      # Single-node cluster: check for already-committed transactions that need consensus
      log = Raft.log(t.raft)
      newest_safe_txn_id = Log.newest_safe_transaction_id(log)

      # Find and send consensus for already-committed transactions
      send_recovery_consensus_for_committed_transactions(t, log, newest_safe_txn_id)
    end

    {:noreply, t}
  end

  @impl true
  def handle_call(:fetch_config, _from, t), do: reply(t, {:ok, t.config})

  def handle_call(:fetch_transaction_system_layout, _from, t) do
    case t.transaction_system_layout do
      nil -> reply(t, {:error, :unavailable})
      transaction_system_layout -> reply(t, {:ok, transaction_system_layout})
    end
  end

  def handle_call({:register_services, services}, from, t) do
    caller_node = Node.self()
    command = Commands.merge_node_resources(caller_node, services, [])

    t
    |> durably_write_service_registration(command, ack_fn(from))
    |> case do
      {:ok, t} -> noreply(t)
      {:error, _reason} = error -> reply(t, error)
    end
  end

  def handle_call({:deregister_services, service_ids}, from, t) do
    command = Commands.deregister_services(service_ids)

    t
    |> durably_write_service_registration(command, ack_fn(from))
    |> case do
      {:ok, t} -> noreply(t)
      {:error, _reason} = error -> reply(t, error)
    end
  end

  def handle_call({:register_node_resources, client_pid, compact_services, capabilities}, from, t) do
    # Always subscribe client for TSL updates (monitor to clean up on death)
    Process.monitor(client_pid)
    updated_state = add_tsl_subscriber(t, client_pid)

    # Expand compact services to full format
    caller_node = node(client_pid)
    expanded_services = expand_compact_services(compact_services, caller_node)

    case updated_state.leader_node do
      node when node == updated_state.my_node ->
        command = Commands.set_node_resources(caller_node, expanded_services, capabilities)

        updated_state
        |> durably_write_service_registration(command, ack_fn(from))
        |> case do
          {:ok, final_state} -> noreply(final_state)
          {:error, _reason} = error -> reply(updated_state, error)
        end

      leader_node ->
        # Not leader - forward async to prevent blocking Raft consensus
        leader_coordinator = {updated_state.otp_name, leader_node}

        GenServer.cast(
          leader_coordinator,
          {:forward_register_node_resources, caller_node, expanded_services, capabilities, from}
        )

        noreply(updated_state)
    end
  end

  def handle_call(:ping, _from, t) do
    leader = if t.leader_node == t.my_node, do: self()
    reply(t, {:pong, t.epoch, leader})
  end

  @impl true
  def handle_info({:raft, :leadership_changed, {:undecided, _raft_epoch}}, t) do
    Logger.info("Bedrock: Received :undecided leadership change")

    t
    |> put_leader_node(:undecided)
    |> put_leader_startup_state(:not_leader)
    |> cleanup_director_on_leadership_loss()
    |> noreply()
  end

  def handle_info({:raft, :leadership_changed, {new_leader, raft_epoch}}, t) do
    trace_election_completed(new_leader)

    updated_t =
      t
      |> put_leader_node(new_leader)
      |> put_epoch(raft_epoch)

    if_result =
      if new_leader == t.my_node do
        # We became leader - start Director immediately
        # TSL is OUTPUT of recovery (not input), config is loaded from object storage at init
        service_count = map_size(updated_t.service_directory)
        trace_leader_ready_starting_director(service_count)

        updated_t
        |> put_leader_startup_state(:leader_ready)
        |> update_recovery_capability_hash()
        |> attempt_director_recovery(:leadership_change)
      else
        # Someone else is leader - clean up director if we have one
        updated_t
        |> put_leader_startup_state(:not_leader)
        |> cleanup_director_on_leadership_loss()
      end

    noreply(if_result)
  end

  def handle_info({:raft, :timer, event}, t) do
    t
    |> update_raft(&Raft.handle_event(&1, event, :timer))
    |> noreply()
  end

  def handle_info({:raft, :send_rpc, event, target}, t) do
    GenServer.cast({t.otp_name, target}, {:raft, :rpc, event, Node.self()})
    noreply(t)
  end

  def handle_info({:raft, :consensus_reached, _log, _durable_txn_id, :behind}, t), do: noreply(t)

  def handle_info({:raft, :consensus_reached, log, durable_txn_id, :latest}, t) do
    trace_consensus_reached(durable_txn_id)

    updated_t = durable_write_completed(t, log, durable_txn_id)

    # Check if capability changes should trigger recovery retry
    updated_t
    |> maybe_retry_recovery_on_capability_change()
    |> noreply()
  end

  def handle_info({:DOWN, _monitor_ref, :process, pid, reason}, t) do
    t
    |> handle_director_failure(pid, reason)
    |> remove_tsl_subscriber(pid)
    |> noreply()
  end

  @impl true
  def handle_cast({:ping, {epoch, director}}, t) when t.epoch == epoch do
    GenServer.cast(director, {:pong, self()})
    noreply(t)
  end

  def handle_cast({:ping, _}, t) do
    noreply(t)
  end

  def handle_cast({:notify_transaction_system_layout, transaction_system_layout}, t) do
    # Direct notification from Director - update state and broadcast to subscribers
    # No Raft consensus needed - TSL is persisted to object storage by Director
    t
    |> Map.put(:old_transaction_system_layout, transaction_system_layout)
    |> put_transaction_system_layout(transaction_system_layout)
    |> put_epoch(transaction_system_layout.epoch)
    |> noreply()
  end

  def handle_cast({:notify_config, config}, t) do
    # Direct notification from Director - update cached config
    # No Raft consensus needed - config is persisted to object storage by Director
    t
    |> put_config(config)
    |> noreply()
  end

  def handle_cast({:forward_register_node_resources, node, services, capabilities, original_from}, t) do
    command = Commands.set_node_resources(node, services, capabilities)

    t
    |> durably_write_service_registration(command, ack_fn(original_from))
    |> case do
      {:ok, updated_state} ->
        noreply(updated_state)

      {:error, _reason} = error ->
        # Reply directly to original caller
        GenServer.reply(original_from, error)
        noreply(t)
    end
  end

  def handle_cast({:raft, :rpc, event, source}, t) do
    t
    |> update_raft(&Raft.handle_event(&1, event, source))
    |> noreply()
  end

  @spec ack_fn(GenServer.from()) :: (term() -> :ok)
  defp ack_fn(from), do: fn result -> GenServer.reply(from, result) end

  @spec init_raft_log(module()) ::
          {:ok, DiskRaftLog.t() | TupleInMemoryLog.t()} | {:error, term()}
  def init_raft_log(cluster) do
    # Use same pattern as logs/storage: get base path from coordinator config
    coordinator_config = Keyword.get(cluster.node_config(), :coordinator, [])

    case Keyword.get(coordinator_config, :path) do
      nil ->
        # No path supplied - use in-memory log (non-persistent)
        {:ok, InMemoryLog.new(:tuple)}

      base_path ->
        # Path supplied - use persistent disk-based log
        working_directory = Path.join(base_path, "raft")
        File.mkdir_p!(working_directory)

        raft_log = DiskRaftLog.new(log_dir: working_directory)
        DiskRaftLog.open(raft_log)
    end
  end

  # Private helper functions

  @spec send_recovery_consensus_for_committed_transactions(
          State.t(),
          Log.t(),
          {non_neg_integer(), non_neg_integer()}
        ) ::
          :ok
  defp send_recovery_consensus_for_committed_transactions(t, log, newest_safe_txn_id) do
    # Find any pending transactions that are actually already committed
    already_committed_txns =
      t.waiting_list
      |> Map.keys()
      |> Enum.filter(fn txn_id ->
        # Transaction is committed if it's <= newest_safe_transaction_id
        txn_id <= newest_safe_txn_id
      end)
      |> Enum.sort()

    if length(already_committed_txns) > 0 do
      Logger.info(
        "Bedrock [#{t.cluster}]: Sending recovery consensus for #{length(already_committed_txns)} already-committed transactions: #{inspect(already_committed_txns)}"
      )

      # Send consensus_reached messages for each already-committed transaction
      Enum.each(already_committed_txns, fn txn_id ->
        send(self(), {:raft, :consensus_reached, log, txn_id, :latest})
      end)
    end
  end

  @spec attempt_director_recovery(State.t(), :leadership_change | :capability_change) :: State.t()
  defp attempt_director_recovery(t, reason) when t.leader_node == t.my_node do
    case t.leader_startup_state do
      :leader_ready ->
        trace_recovery_retry_attempt(reason)

        case try_to_start_director(t) do
          %{director: :unavailable} = failed_state ->
            # Recovery failed - mark as such and don't retry automatically
            trace_recovery_failed(:director_start_failed)
            put_leader_startup_state(failed_state, :recovery_failed)

          successful_state ->
            # Recovery succeeded
            successful_state
        end

      :recovery_failed ->
        # Don't retry if we've already failed - wait for meaningful capability changes
        case reason do
          :capability_change ->
            # Capability change detected - worth retrying
            attempt_director_recovery(put_leader_startup_state(t, :leader_ready), reason)

          _ ->
            # Other reasons don't trigger retry from failed state
            t
        end

      :not_leader ->
        # Not leader - shouldn't attempt recovery
        t
    end
  end

  defp attempt_director_recovery(t, _reason), do: t

  @spec maybe_retry_recovery_on_capability_change(State.t()) :: State.t()
  defp maybe_retry_recovery_on_capability_change(t) when t.leader_node == t.my_node do
    case check_for_recovery_capability_changes(t) do
      {:changed, updated_t} ->
        trace_recovery_capability_change_detected()
        attempt_director_recovery(updated_t, :capability_change)

      {:unchanged, updated_t} ->
        updated_t
    end
  end

  defp maybe_retry_recovery_on_capability_change(t), do: t

  @spec expand_compact_services([{String.t(), atom(), atom()}], node()) :: [
          Commands.service_info()
        ]
  defp expand_compact_services(compact_services, caller_node) do
    Enum.map(compact_services, fn {service_id, kind, name} ->
      {service_id, kind, {name, caller_node}}
    end)
  end

  # Object Storage loading functions

  @spec load_state_from_object_storage(module()) ::
          {:ok, {Bedrock.epoch() | nil, map() | nil, map() | nil}} | {:error, term()}
  defp load_state_from_object_storage(cluster) do
    case get_object_storage_backend(cluster) do
      {:ok, backend} ->
        case fetch_bootstrap_data(backend, cluster) do
          {:ok, data} ->
            case parse_bootstrap_data(data, cluster) do
              {:ok, bootstrap} ->
                epoch = bootstrap.epoch
                config = build_config_from_bootstrap(bootstrap, cluster)
                old_tsl = build_old_tsl_from_bootstrap(bootstrap)

                Logger.info("Bedrock [#{cluster}]: Loaded cluster bootstrap from object storage (epoch: #{epoch})")
                {:ok, {epoch, config, old_tsl}}

              {:error, reason} ->
                {:error, {:bootstrap_parse_failed, reason}}
            end

          {:error, :not_found} ->
            admitted_first_boot_state(backend, cluster)

          {:error, reason} ->
            {:error, {:bootstrap_fetch_failed, reason}}
        end

      {:error, reason} ->
        {:error, {:object_storage_unavailable, reason}}
    end
  rescue
    exception ->
      {:error, {:bootstrap_load_exception, exception.__struct__, Exception.message(exception)}}
  catch
    :exit, reason -> {:error, {:bootstrap_load_exit, reason}}
  end

  defp admitted_first_boot_state(backend, cluster) do
    case validated_first_boot_admission(backend, cluster) do
      :ok -> {:ok, {nil, nil, nil}}
      {:error, reason} -> {:error, {:bootstrap_missing_without_validated_first_boot_admission, reason}}
    end
  end

  defp validated_first_boot_admission({module, config}, cluster) when is_atom(module) and is_list(config) do
    if function_exported?(module, :first_boot_admission_status, 2) do
      case module.first_boot_admission_status(config, cluster.node_config()) do
        {:ok, :admitted} -> :ok
        {:ok, :not_required} -> {:error, :first_boot_admission_not_validated}
        {:error, reason} -> {:error, reason}
        other -> {:error, {:invalid_first_boot_admission_status, other}}
      end
    else
      {:error, :first_boot_admission_validator_missing}
    end
  end

  defp validated_first_boot_admission(_backend, _cluster), do: {:error, :invalid_first_boot_admission_backend}

  defp fetch_bootstrap_data(backend, cluster) do
    case ObjectStorage.get(backend, "bootstrap") do
      {:ok, data} ->
        {:ok, data}

      {:error, :not_found} ->
        Logger.info(
          "Bedrock [#{cluster}]: No cluster bootstrap in object storage; requiring validated first-boot admission"
        )

        {:error, :not_found}

      {:error, reason} ->
        Logger.warning("Bedrock [#{cluster}]: Failed to load cluster bootstrap from object storage: #{inspect(reason)}")

        {:error, reason}
    end
  end

  defp parse_bootstrap_data(data, cluster) do
    case ClusterBootstrap.read(data) do
      {:ok, bootstrap} ->
        {:ok, bootstrap}

      {:error, reason} ->
        Logger.warning("Bedrock [#{cluster}]: Failed to parse cluster bootstrap: #{inspect(reason)}")
        {:error, reason}
    end
  end

  # Build a Config struct from ClusterBootstrap data
  defp build_config_from_bootstrap(bootstrap, cluster) do
    {:ok, coordinator_nodes} = cluster.fetch_coordinator_nodes()

    %{
      coordinators: coordinator_nodes,
      parameters: build_parameters(bootstrap[:parameters], coordinator_nodes),
      policies: build_policies(bootstrap[:policies])
    }
  end

  defp build_parameters(nil, coordinator_nodes), do: default_parameters(coordinator_nodes)

  defp build_parameters(params, coordinator_nodes) do
    defaults = default_parameters(coordinator_nodes)

    %{
      nodes: coordinator_nodes,
      desired_coordinators: params[:desired_coordinators] || defaults.desired_coordinators,
      desired_logs: params[:desired_logs] || defaults.desired_logs,
      desired_replication_factor: params[:desired_replication_factor] || defaults.desired_replication_factor,
      desired_commit_proxies: params[:desired_commit_proxies] || defaults.desired_commit_proxies,
      desired_read_version_proxies: params[:desired_read_version_proxies] || defaults.desired_read_version_proxies,
      ping_rate_in_hz: params[:ping_rate_in_hz] || defaults.ping_rate_in_hz,
      retransmission_rate_in_hz: params[:retransmission_rate_in_hz] || defaults.retransmission_rate_in_hz,
      transaction_window_in_ms: params[:transaction_window_in_ms] || defaults.transaction_window_in_ms
    }
  end

  defp default_parameters(coordinator_nodes) do
    %{
      nodes: coordinator_nodes,
      desired_coordinators: length(coordinator_nodes),
      desired_logs: 1,
      desired_replication_factor: 1,
      desired_commit_proxies: 1,
      desired_read_version_proxies: 1,
      ping_rate_in_hz: 10,
      retransmission_rate_in_hz: 20,
      transaction_window_in_ms: 5_000
    }
  end

  defp build_policies(nil), do: %{allow_volunteer_nodes_to_join: true}
  defp build_policies(p), do: %{allow_volunteer_nodes_to_join: p[:allow_volunteer_nodes_to_join] || false}

  # Build old_transaction_system_layout from ClusterBootstrap logs
  # Recovery only uses the logs field to determine which logs to copy from
  defp build_old_tsl_from_bootstrap(bootstrap) do
    logs =
      Map.new(bootstrap[:logs] || [], fn log_info ->
        # LogDescriptor is just [range_tag] - a list of shard tags
        {log_info[:id], log_info[:shard_tags] || []}
      end)

    %{logs: logs}
  end

  @spec get_object_storage_backend(module()) :: {:ok, ObjectStorage.backend()} | {:error, :no_object_storage}
  defp get_object_storage_backend(cluster) do
    node_config = cluster.node_config()

    # Check for explicit object_storage config
    case Keyword.fetch(node_config, :object_storage) do
      {:ok, backend} ->
        {:ok, backend}

      :error ->
        # Derive from path config (same logic as cluster_supervisor and persistence_phase)
        derive_object_storage_from_path(node_config)
    end
  end

  defp derive_object_storage_from_path(node_config) do
    # Try to find a path from any capability config
    path =
      Enum.find_value([:coordinator, :log, :storage, :materializer, :coordination], fn capability ->
        node_config
        |> Keyword.get(capability, [])
        |> Keyword.get(:path)
      end)

    if path do
      object_storage_root = Path.join(path, "object_storage")
      backend = ObjectStorage.backend(LocalFilesystem, root: object_storage_root)
      {:ok, backend}
    else
      {:error, :no_object_storage}
    end
  end
end
