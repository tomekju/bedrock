defmodule Bedrock.Cluster.Link.FetchCoordinatorTest do
  use ExUnit.Case, async: true

  alias Bedrock.Cluster.Link.Server
  alias Bedrock.Cluster.Link.State

  describe "get_known_coordinator/0" do
    test "returns error when coordinator unavailable" do
      state = %State{
        node: Node.self(),
        cluster: DefaultTestCluster,
        known_coordinator: :unavailable
      }

      assert {:reply, {:error, :unavailable}, ^state} =
               Server.handle_call(:get_known_coordinator, self(), state)
    end

    test "returns coordinator when available" do
      coordinator_ref = :test_coordinator_ref

      state = %State{
        node: Node.self(),
        cluster: DefaultTestCluster,
        known_coordinator: coordinator_ref
      }

      assert {:reply, {:ok, ^coordinator_ref}, ^state} =
               Server.handle_call(:get_known_coordinator, self(), state)
    end
  end

  test "Coordinator failure clears the cached runnable layout before discovery" do
    coordinator = start_supervised!({Agent, fn -> nil end})

    state = %State{
      node: Node.self(),
      cluster: DefaultTestCluster,
      known_coordinator: coordinator,
      transaction_system_layout: %{id: "stale-layout"}
    }

    assert {:noreply,
            %State{
              known_coordinator: :unavailable,
              transaction_system_layout: nil
            }, {:continue, :find_a_live_coordinator}} =
             Server.handle_info(
               {:DOWN, make_ref(), :process, coordinator, :coordinator_restarted},
               state
             )
  end
end
